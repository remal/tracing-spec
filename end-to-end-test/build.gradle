import java.util.Map.Entry
import java.util.regex.Pattern

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-library'
apply plugin: 'name.remal.test-fixtures'

dependencies {
    implementation project(':utils-test')
    implementation project(':tracing-spec-model')
    implementation project(':tracing-spec-spring-sleuth')
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'
    implementation 'io.zipkin.reporter2:zipkin-sender-urlconnection'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-kafka'
    implementation 'org.testcontainers:kafka'
    implementation 'com.github.javafaker:javafaker'
}


forBuildOnLocal {
    dependencies {
        //runtimeOnly 'io.projectreactor:reactor-tools'
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

configurations.all {
    exclude group: 'org.springframework.cloud', module: 'spring-cloud-netflix-archaius'
    exclude group: 'org.springframework.cloud', module: 'spring-cloud-netflix-ribbon'
    exclude group: 'io.github.openfeign', module: 'feign-hystrix'
}

Map<String, Pattern> bannedDependencyTokens = [
    'archaius',
    'ribbon',
    'hystrix',
].collectEntries { [it, Pattern.compile('\\b' + Pattern.quote(it) + '\\b')] }
configurations.all {
    resolutionStrategy {
        componentSelection {
            all {
                for (Entry<String, Pattern> entry : bannedDependencyTokens.entrySet()) {
                    Pattern pattern = entry.value
                    if (pattern.matcher("${candidate.group}:${candidate.module}").find()) {
                        reject("Banned token: '${entry.key}'")
                        return
                    }
                }
            }
        }
    }
}
