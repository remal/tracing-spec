import name.remal.version.Version

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-library'
apply plugin: 'name.remal.classes-relocation'
apply plugin: 'maven-publish'

dependencies {
    relocateClasses project(':utils-okhttp')
    relocateClasses project(':utils-gson')

    annotationProcessing('org.immutables:gson') { exclude group: 'com.google.code.gson' }

    api project(':tracing-spec-retriever')
    api 'org.apache.logging.log4j:log4j-api'


    integrationImplementation 'io.jaegertracing:jaeger-thrift'
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

SourceSet versionTestSourceSet = sourceSets.integration
String baseVersionTestTaskName = versionTestSourceSet.getTaskName('test', null)

Task testSourceSetVersionsTask = tasks.create("${baseVersionTestTaskName}Versions") {
    group = 'verification'
    dependsOn(baseVersionTestTaskName)
    tasks.allTests.dependsOn(it)
}

tasks.create("testAllVersions") {
    dependsOn(testSourceSetVersionsTask)
}

SortedMap<Version, String> dockerTagImages = dockerImages.getJaegerVersionsToImage()

dockerTagImages.forEach { Version tagName, String imageName ->
    TaskProvider<Task> testVersionTask = tasks.register("$baseVersionTestTaskName-$tagName", Test) {
        dependsOn(versionTestSourceSet.classesTaskName)

        systemProperty('jaeger-image', imageName)
        systemProperty('jaeger-image-tag', tagName)

        Test baseTestTask = tasks[baseVersionTestTaskName]
        group = baseTestTask.group

        onlyIf {
            testClassesDirs = baseTestTask.testClassesDirs
            classpath = baseTestTask.classpath

            filter {
                includeTestsMatching('*VersionTest')
                failOnNoMatchingTests = true
            }

            return true
        }

        doFirst {
            String currentImage = systemProperties['jaeger-image']
            String currentTag = systemProperties['jaeger-image-tag']
            println "Testing with Docker image $currentImage:$currentTag"
        }
    }

    testSourceSetVersionsTask.dependsOn(testVersionTask)
}
